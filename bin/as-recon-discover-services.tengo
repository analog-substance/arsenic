#! /usr/bin/env arsenic

cobra := import("cobra")
os := import("os")
os2 := import("os2")
filepath := import("filepath")
nmap := import("nmap")
script := import("script")
log := import("log")

topPorts := 0
explicitPorts := []
explicitOutput := ""
targets := []

setupHost := func(target) {
  baseDir := ""
  if filepath.dir_exists("hosts") {
    baseDir = filepath.join("hosts", target)
  }

  err := os.mkdir_all(filepath.join(baseDir, "loot"), 0755)
  check_err(err)

  err = os.mkdir_all(filepath.join(baseDir, "recon"), 0755)
  check_err(err)

  return baseDir
}

defaultScanner := func(target, args) {
  scanner := nmap.scanner().
    stylesheet("/static/nmap.xsl").
    open().
    Pn().
    n().
    system_dns().
    targets(target)
  if len(args) > 0 {
    scanner.custom_args(args...)
  }

  return scanner
}

rootCmd := cobra.root_cmd(script.name, "Run nmap scans on targets")
rootCmd.enable_completion()

rootCmd.persistent_flags.string_slicep("targets", "t", [], "The target to scan. Can be used multiple times to specify multiple targets")
rootCmd.persistent_flags.string_slicep("ports", "p", [], "The specific ports to scan. Can be used multiple times to specify multiple targets")
rootCmd.persistent_flags.stringp("target-file", "f", "", "The file containing the targets to scan.")
rootCmd.persistent_flags.stringp("output", "o", "", "The name of the output file without the file extension")
rootCmd.persistent_flags.int("top", 0, "Scan # most common ports")

rootCmd.set_persistent_pre_run(func(cmd, args) {
  targets = cmd.flags.get_string_slice("targets")
  file := cmd.flags.get_string("target-file")

  if file != "" {
    lines := os2.read_file_lines(file)
    check_err(lines)

    for line in lines {
      targets = append(targets, line)
    }
  }

  for line in os2.read_stdin() {
    targets = append(targets, line)
  }

  if len(targets) == 0 {
    script.fatal("No targets specified")
  }

  topPorts = cmd.flags.get_int("top")
  explicitPorts = cmd.flags.get_string_slice("ports")
  explicitOutput = cmd.flags.get_string("output")
})

// TCP Command

tcpCmd := cobra.cmd("tcp", "Run TCP scans")
rootCmd.add_command(tcpCmd)

tcpCmd.set_run(func(cmd, args){
  topScan := func(target) {
    log.msg(format("Scanning %s: top %d TCP ports", target, topPorts))

    output := format("nmap-top-%d-tcp", topPorts)
    if explicitOutput != "" {
      output = explicitOutput
    }

    scanner := defaultScanner(target, args).
      sudo().
      privileged().
      oA(filepath.join("recon", output)).
      host_timeout("30m"). // Make an argument/configurable
      A().
      top_ports(topPorts)
    
    run := scanner.run()
    check_err(run)
  }

  quickScan := func(target) {
    output := "nmap-quick-tcp"
    if explicitOutput != "" {
      output = format("%s-quick", explicitOutput)
    }

    scanner := defaultScanner(target, args).
      T3().
      host_timeout("30m"). // Make an argument/configurable
      ports("-").
      oA(filepath.join("recon", output))

    run := scanner.run()
    if is_error(run) {
      log.warn(format("Error running quick TCP nmap scan: %v", run))
      return []
    }

    return run.ports
  }

  serviceScan := func(target, ports) {
    log.msg(format("Version scanning %v", ports))

    output := "nmap-tcp"
    if explicitOutput != "" {
      output = explicitOutput
    }

    scanner := defaultScanner(target, args).
      sudo().
      privileged().
      oA(filepath.join("recon", output)).
      host_timeout("30m"). // Make an argument/configurable
      A().
      ports(ports...)
    
    run := scanner.run()
    check_err(run)
  }

  fullScan := func(target) {
    log.msg(format("Scanning %s: all TCP ports", target))

    ports := quickScan(target)
    if len(ports) == 0 {
      log.msg(format("%s: No open ports", target))
      return
    }

    serviceScan(target, ports)
  }

  for target in targets { 
    baseDir := setupHost(target)

    err := os2.temp_chdir(baseDir, func(){
      if topPorts > 0 {
        topScan(target)
      } else if len(explicitPorts) > 0 {
        log.msg(format("Scanning %s", target))
        serviceScan(target, explicitPorts)
      } else {
        fullScan(target)
      }
    })
    check_err(err)
  }
})

// UDP Command

udpCmd := cobra.cmd("udp", "Run UDP scans")
rootCmd.add_command(udpCmd)

udpCmd.set_run(func(cmd, args){
  udpScanner := func(target) {
    return defaultScanner(target, args).
      sudo().
      privileged().
      sU().
      sV().
      min_rate(1000).
      max_rtt_timeout("100ms").
      version_intensity(0)
  }

  topScan := func(target) {
    log.msg(format("Scanning %s: top %d UDP ports", target, topPorts))

    output := format("nmap-top-%d-udp", topPorts)
    if explicitOutput != "" {
      output = explicitOutput
    }

    scanner := udpScanner(target).
      top_ports(topPorts).
      oA(filepath.join("recon", output))
    
    run := scanner.run()
    check_err(run)
  }

  serviceScan := func(target, ports) {
    output := "nmap-udp"
    if explicitOutput != "" {
      output = explicitOutput
    }

    scanner := udpScanner(target).
      ports(ports...).
      oA(filepath.join("recon", output))
    
    run := scanner.run()
    check_err(run)
  }

  fullScan := func(target) {
    log.msg(format("Scanning %s: all UDP ports", target))
    serviceScan(target, ["-"])
  }

  for target in targets {
    baseDir := setupHost(target)

    err := os2.temp_chdir(baseDir, func(){
      if topPorts > 0 {
        topScan(target)
      } else if len(explicitPorts) > 0{
        serviceScan(target, explicitPorts)
      } else {
        fullScan(target)
      }
    })
    check_err(err)
  }
})

err := rootCmd()
check_err(err)
