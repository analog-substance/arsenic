#!/usr/bin/env arsenic

fmt := import("fmt")
os := import("os")
os2 := import("os2")
filepath := import("filepath")
slice := import("slice")
arsenic := import("arsenic")
script := import("script")
log := import("log")
git := import("git")
exec := import("exec")
cobra := import("cobra")
check_err := import("check_err")

getHosts := func(flags) {
  err := git.pull()
  check_err(err)
  
  hosts := []
  allHosts := arsenic.hosts(flags)
  check_err(allHosts)

  for host in allHosts {
    if !host.has_any_port() {
      continue
    }

    files := host.files(filepath.from_slash("recon/nuclei-cves.txt"))
    if len(files) > 0 {
      continue
    }

    hosts = append(hosts, host)
  }

  return hosts
}

flags := []

rootCmd := cobra.root_cmd("as-nuclei-cves.tengo", "Run Nuclei CVE templates against current scope")
rootCmd.persistent_flags.string_slicep("flags", "f", [], "Filter hosts by flags")

rootCmd.set_persistent_pre_run(func(cmd, args) {
  flags = cmd.flags.get_string_slice("flags")

  err := git.pull()
  check_err(err)
})

listCmd := cobra.cmd("list", "Run Nuclei CVE templates on hosts that need it.")
rootCmd.add_command(listCmd)

listCmd.set_run(func(cmd, args) {
	hosts := getHosts(flags)
  for host in hosts {
    fmt.println(host.name)
  }
})

runCmd := cobra.cmd("run", "Lists the hosts that need to run the Nuclei CVE templates.")
runCmd.flags.stringp("host", "H", "", "Host to run templates on.")
runCmd.flags.stringp("proxy", "p", "", "The http/socks5 proxy to use")
rootCmd.add_command(runCmd)

runCmd.set_run(func(cmd, args) {
  targetHost := undefined
  targetHostName := cmd.flags.get_string("host")
  proxy := cmd.flags.get_string("proxy")

  manual := targetHostName != ""

  autoSelect := func() {
    targetHost = slice.rand_item(getHosts(flags))
    if !targetHost {
      script.stop("No host found")
    }
    
    log.warn(format("Auto selected %s", targetHost.name))
  }

  runTemplates := func(host) {
    if !host {
      return
    }

    log.msg(format("Nuclei / CVEs / %s/ checking", host.name))

    reconPath := filepath.join(host.dir, "recon")

    files := host.files(filepath.join(reconPath, "nuclei-cves.txt"))
    check_err(files)

    if len(files) != 0 {
      return
    }

    log.info(format("Nuclei / CVEs / %s / preparing", host.name))

    os.mkdir_all(reconPath, 0755)

    outputName := filepath.join(reconPath, "nuclei-cves.txt")

    err := git.lock(outputName, format("Nuclei CVEs lock: %s", host.name))
    check_err(err)

    log.info(format("Nuclei / CVEs / %s / running", host.name))
    
    tempDir := os2.mkdir_temp(os.temp_dir(), "nuclei-technologies-*")
    check_err(tempDir)

    nucleiArgs := ["nuclei", "-project", "-project-path", tempDir, "-t", "cves", "-o", outputName, "-ni"]
    if proxy != "" {
      nucleiArgs = append(nucleiArgs, "-p", proxy)
    }

    hostURLs := host.urls()
    check_err(hostURLs)
    if is_error(hostURLs) {
      os.remove_all(tempDir)
      script.stop(hostURLs)
    }

    for u in hostURLs {
      nucleiArgs = append(nucleiArgs, "-u", u)
    }

    err = exec.run_with_sig_handler(nucleiArgs...)
    os.remove_all(tempDir)
    check_err(err)

    err = git.commit(".", format("Nuclei CVEs complete: %s", host.name))
    check_err(err)

    log.info(format("Nuclei / CVEs / %s / complete", host.name))
  }

  if manual {
    targetHost = arsenic.host(targetHostName)
    check_err(targetHost)
  } else {
    log.warn("no args found, autodetecting")

    autoSelect()
  }

  for {
    runTemplates(targetHost)

    if manual {
      break
    }

    autoSelect()
  }
})

err := rootCmd()
check_err(err)
