fmt := import("fmt")
text := import("text")
os := import("os")
filepath := import("filepath")
sort := import("sort")
arsenic := import("arsenic")
url := import("url")
engine := import("engine")
times := import("times")
log := import("log")
rand := import("rand")
git := import("git")

wordlistFile := ""
wordlistName := ""
manual := true

action := "scan"
if args.action {
  action = args.action
}

argHost := ""
if args.host {
  argHost = args.host
}

argURL := ""
if args.url{
  argURL = args.url
}


genOutputFileName := func(rawURL) {
  rawURL = text.replace(rawURL, "://", ".", -1)
  rawURL = text.replace(rawURL, "/", ".", -1)
  return fmt.sprintf("ffuf.%s.%s.json", rawURL, wordlistName)
}

getHostURLs := func() {
  urls := arsenic.host_urls("http")
  if is_error(urls) {
    engine.stop(urls)
  }

  hostURLs := []
  for rawURL in urls {
    hostname := url.hostname(rawURL)
    if is_error(hostname) {
      engine.stop(hostname)
    }

    hostPath := arsenic.host_path(hostname)
    if is_error(hostPath) {
      engine.stop(hostPath)
    }

    host := filepath.base(hostPath)

    outputFile := genOutputFileName(rawURL)
    
    if !filepath.exists(filepath.join(hostPath, "recon", outputFile)) {
      hostURLs = append(hostURLs, fmt.sprintf("%s %s", host, rawURL))
    }
  }

  return hostURLs
}

randElement := func(slice) {
  if len(slice) == 0 {
    return undefined
  }

  r := rand.rand(times.time_unix_nano(times.now()))
  i := r.intn(len(slice))
  return slice[i]
}

scanHost := func(host, rawURL) {
  log.msg(format("Content Discovery / %s / %s / checking", host, rawURL))
  // check if host is a draft

  // do the following only if the host isn't a draft
  log.info(format("Content Discovery / %s / %s / preparing", host, rawURL))

  previousDir := os.getwd()
  if is_error(previousDir) {
    engine.stop(previousDir)
  }

  err := os.chdir(filepath.join("hosts", host))
  if is_error(err) {
    engine.stop(err)
  }
  
  outputFile := genOutputFileName(rawURL)

  git.pull()

  outputPath := filepath.join("recon", outputFile)
  if !filepath.exists(outputPath) {
    log.msg(format("Scanning %s %s", host, rawURL))

    // check if url is S3 bucket

    err = git.lock(outputPath, format("ffuf lock: %s", rawURL))
    if is_error(err) {
      engine.stop(err)
    }

    log.info(format("Content Discovery / %s / %s / running", host, rawURL))

    err = arsenic.ffuf("-a", "Firefox", "-u", rawURL, "-w", wordlistFile, "-o", outputFile)
    if is_error(err) {
      engine.stop(err)
    }

    log.info(format("Content Discovery / %s / %s / complete", host, rawURL))
  }

  err = git.commit(".", format("ffuf complete: %s", rawURL))
  if is_error(err) {
    engine.stop(err)
  }

  err = os.chdir(previousDir)
  if is_error(err){
    engine.stop(err)
  }
}

wordlistFile = filepath.abs("recon/wordlist-content-discover.txt")
if is_error(wordlistFile) {
  engine.stop(wordlistFile)
}

if !filepath.exists(wordlistFile) {
  log.info("Generating wordlist...")
  err := arsenic.gen_wordlist("web-content", wordlistFile)
  if is_error(err) {
    engine.stop(err)
  }
}

wordlistName = filepath.base(wordlistFile)
wordlistName = text.trim_suffix(wordlistName, filepath.ext(wordlistName))

err := git.pull()
if is_error(err) {
  engine.stop(err)
}

if action == "list" {
  hostURLs := getHostURLs()
  for hostURL in hostURLs {
    fmt.println(hostURL)
  }
  engine.stop()
}

autoSelect := func() {
  elem := randElement(getHostURLs())
  if !elem {
    files := arsenic.locked_files("hosts/*/recon/ffuf*.json")
    if is_error(files) {
      engine.stop(files)
    }

    if len(files) > 0 {
      log.warn("other ffufs are still running... lets wait before continuing")
      engine.stop()
    }

    engine.stop("No host found")
  }
  
  argHost = text.fields(elem)[0]
  argURL = text.fields(elem)[1]

  log.warn(format("Auto selected %s %s", argHost, argURL))
}

if argHost == "" {
  manual = false
  log.warn("no args found, autodetecting")

  autoSelect()
}

for {
  scanHost(argHost, argURL)
  if manual {
    break
  }

  autoSelect()
}
