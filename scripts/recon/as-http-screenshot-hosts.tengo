#!/usr/bin/env arsenic

fmt := import("fmt")
text := import("text")
os := import("os")
filepath := import("filepath")
slice := import("slice")
arsenic := import("arsenic")
script := import("script")
times := import("times")
log := import("log")
git := import("git")
os2 := import("os2")
exec := import("exec")
cobra := import("cobra")

getURLs := func(host) {
  re := text.re_compile(`\.(png|jpe?g|ico|css|gif|js)$`)

  urls := []
  codes := [200]
  patterns := ["recon/gobuster.*.txt", "recon/ffuf.*.json", "recon/dirb.*.txt"]
  
  discoveredURLs := host.content_discovery_urls(patterns, codes)
  if is_error(discoveredURLs) {
    script.stop(discoveredURLs)
  }

  for u in discoveredURLs {
    if re.match(u) {
      continue
    }

    urls = append(urls, u)
  }

  urls = slice.unique(urls)
  if is_error(urls) {
    script.stop(urls)
  }

  return urls
}

getHosts := func(flags) {
  err := git.pull()
  if is_error(err) {
    script.stop(err)
  }

  allHosts := arsenic.hosts(append(flags, "web-content"))
  if is_error(allHosts) {
    script.stop(allHosts)
  }

  hosts := []
  for host in allHosts {
    exists := host.file_exists(filepath.join("recon", "wordlist-content-discover.txt"))
    if is_error(exists) {
      script.stop(exists)
    }

    if exists {
      continue
    }

    previousDir := os.getwd()
    if is_error(previousDir) {
      script.stop(previousDir)
    }

    err = os.chdir(host.dir)
    if is_error(err) {
      script.stop(err)
    }

    if len(getURLs(host)) > 0 {
      hosts = append(hosts, host)
    }

    err = os.chdir(previousDir)
    if is_error(err){
      script.stop(err)
    }
  }
  return hosts
}

flags := []

rootCmd := cobra.root_cmd("as-http-screenshot-hosts.tengo", "Take screenshots of URLs found during content enumeration scans.")
rootCmd.persistent_flags.string_slicep("flags", "f", [], "Filter hosts by flags")

rootCmd.set_persistent_pre_run(func(cmd, args) {
  flags = cmd.flags.get_string_slice("flags")

  err := git.pull()
  if is_error(err) {
    script.stop(err)
  }
})

listCmd := cobra.cmd("list", "Lists the hosts that need screenshots.")
rootCmd.add_command(listCmd)

listCmd.set_run(func(cmd, args) {
	hosts := getHosts(flags)
  for h in hosts {
    fmt.println(h.name)
  }
})

scanCmd := cobra.cmd("scan", "Scan hosts that need screenshots.")
scanCmd.flags.stringp("host", "H", "", "Host to scan.")
scanCmd.flags.stringp("proxy", "p", "", "Proxy to use for aquatone.")
rootCmd.add_command(scanCmd)

scanCmd.set_run(func(cmd, args) {
  targetHost := undefined
  targetHostName := cmd.flags.get_string("host")
  proxy := cmd.flags.get_string("proxy")

  manual := targetHostName != ""

  autoSelect := func() {
    targetHost = slice.rand_item(getHosts(flags))
    if !targetHost {
      files := arsenic.locked_files("hosts/*/recon/wordlist-content-discover.txt")
      if is_error(files) {
        script.stop(files)
      }

      if len(files) > 0 {
        log.warn("other aquatones are still running... lets wait before continuing")
        script.stop()
      }

      script.stop("No host found")
    }

    log.warn(format("Auto selected %s", targetHost.name))
  }

  scanHost := func(host) {
    log.msg(format("Aquatone / %s / checking", host.name))

    contentDiscoverPath := filepath.join("recon", "wordlist-content-discover.txt")
    exists := host.file_exists(contentDiscoverPath)
    if is_error(exists) {
      script.stop(exists)
    }

    if !exists {
      log.msg(format("Aquatone / %s / preparing", host.name))

      previousDir := os.getwd()
      if is_error(previousDir) {
        script.stop(previousDir)
      }

      err := os.chdir(host.dir)
      if is_error(err) {
        script.stop(err)
      }

      err = git.lock(contentDiscoverPath, format("Aquatone lock: %s", host.name))
      if is_error(err) {
        script.stop(err)
      }

      err = os2.write_file(contentDiscoverPath, text.join(getURLs(host), "\n"))
      if is_error(err) {
        script.stop(err)
      }

      log.msg(format("Aquatone / %s / running", host.name))

      cmdArgs := ["-out", filepath.join("recon", "aquatone-wordlist-content-discover")]
      if proxy != "" {
        cmdArgs = append(cmdArgs, "-proxy", proxy)
      }

      cmd := exec.cmd("aquatone", cmdArgs...)
      if is_error(cmd) {
        script.stop(cmd)
      }

      err = cmd.set_file_stdin(contentDiscoverPath)
      if is_error(err) {
        script.stop(err)
      }

      err = cmd.run()
      if is_error(err) {
        script.stop(err)
      }

      htmlFiles := filepath.glob("**/*.html")
      if is_error(htmlFiles) {
        script.stop(htmlFiles)
      }

      for file in htmlFiles {
        err = os2.regex_replace_file(file, `^\xEF\xBB\xBF`, "")
        if is_error(err) {
          script.stop(err)
        }
      }

      err = git.commit(".", format("Aquatone complete: %s", host.name))
      if is_error(err) {
        script.stop(err)
      }

      err = os.chdir(previousDir)
      if is_error(err){
        script.stop(err)
      }
    }
    
    log.msg(format("Aquatone / %s / complete", host.name))
    times.sleep(10 * times.second)
  }

  if manual {
    targetHost = arsenic.host(targetHostName)
    if is_error(targetHost) {
      script.stop(targetHost)
    }
  } else {
    log.warn("no args found, autodetecting")

    autoSelect()
  }

  for {
    scanHost(targetHost)

    if manual {
      break
    }

    autoSelect()
  }
})

err := rootCmd()
if is_error(err) {
	script.stop(err)
}
